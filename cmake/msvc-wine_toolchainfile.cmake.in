# This file is Public Domain, do as you please

if(CMAKE_VERSION VERSION_GREATER_EQUAL "3.20")
  set(_prefix "${CMAKE_CURRENT_LIST_DIR}/@RELPATH@/")
  cmake_path(ABSOLUTE_PATH _prefix NORMALIZE)
else()
  get_filename_component(_prefix "${CMAKE_CURRENT_LIST_DIR}/@RELPATH@/" ABSOLUTE)
endif()

if(NOT CMAKE_SYSTEM_NAME)
  set(CMAKE_SYSTEM_NAME Windows)
endif()
if(NOT CMAKE_SYSTEM_PROCESSOR)
  set(CMAKE_SYSTEM_PROCESSOR ${CMAKE_HOST_SYSTEM_PROCESSOR})
endif()

# try to match the known values
# https://learn.microsoft.com/en-us/windows/win32/winprog64/wow64-implementation-details
# https://learn.microsoft.com/en-us/windows/win32/api/sysinfoapi/ns-sysinfoapi-system_info
if(CMAKE_SYSTEM_PROCESSOR MATCHES "^[xX]86_64$" OR CMAKE_SYSTEM_PROCESSOR MATCHES "^[xX]64$")
  set(CMAKE_SYSTEM_PROCESSOR AMD64)
elseif(CMAKE_SYSTEM_PROCESSOR MATCHES "^i[3-6]86$" OR CMAKE_SYSTEM_PROCESSOR MATCHES "^[xX]86$")
  set(CMAKE_SYSTEM_PROCESSOR x86)
elseif(CMAKE_SYSTEM_PROCESSOR STREQUAL "aarch64")
  set(CMAKE_SYSTEM_PROCESSOR ARM64)
elseif(CMAKE_SYSTEM_PROCESSOR MATCHES "^armv[4-8]$")
  set(CMAKE_SYSTEM_PROCESSOR ARM)
endif()

string(TOLOWER "${CMAKE_SYSTEM_PROCESSOR}" _arch)
if(_arch STREQUAL "amd64")
  set(_arch x64)
endif()

# set these before find_program!
set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE ONLY)

set(CMAKE_ASM_COMPILER "${_prefix}bin/${_arch}/cl")
set(CMAKE_C_COMPILER "${_prefix}bin/${_arch}/cl")
set(CMAKE_CXX_COMPILER "${_prefix}bin/${_arch}/cl")
set(CMAKE_RC_COMPILER "${_prefix}bin/${_arch}/rc")
set(CMAKE_LINKER "${_prefix}bin/${_arch}/link")

unset(_arch)
unset(_prefix)

# add these in your project to automatically configure the compiler
# see https://github.com/mstorsjo/msvc-wine#does-it-work-with-cmake
# if(POLICY CMP0141)
#   cmake_policy(SET CMP0141 NEW)
# endif()

if(POLICY CMP0141 AND NOT CMAKE_MSVC_DEBUG_INFORMATION_FORMAT)
  set(CMAKE_MSVC_DEBUG_INFORMATION_FORMAT Embedded CACHE STRING "Debug Format")
endif()

if(NOT MSVC_REDIST_DIR)
  # Used by https://cmake.org/cmake/help/latest/module/InstallRequiredSystemLibraries.html
  file(GLOB _respath "${_prefix}VC/Redist/MSVC/*.*.*")
  set(MSVC_REDIST_DIR "${_respath}" CACHE STRING "Redist DIR")
endif()
unset(_respath)
